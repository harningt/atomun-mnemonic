import org.ajoberstar.gradle.git.release.opinion.Strategies

plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '0.6'
    id 'org.ajoberstar.defaults' version '0.4.0'
    id 'com.jfrog.artifactory' version '3.0.1'
}

group = 'us.eharning.atomun'
description = 'Utility library implementing mnemonic <-> bytes conversions.'
sourceCompatibility = 1.7

defaults {
    id = 'harningt'

    bintrayRepo = 'atomun'
    bintrayLabels = ['bitcoin', 'electrum', 'bip-0039']

    developers = [
            [id: 'harningt', name: 'Thomas Harning Jr', email: 'harningt@gmail.com']
    ]

    // used by license plugin
    copyrightYears = '2014'
}

configurations {
    // use local groovy
    all*.exclude group: 'org.codehaus.groovy'
}

project.afterEvaluate {
    // Disable signing because this requires serverside key
    project.bintray.pkg.version.gpg.sign = false
}

release {
    // Adds SNAPSHOT strategy to allowed list to be used by Travis-CI
    versionStrategy Strategies.SNAPSHOT
}

def isSnapshot = hasProperty('release.stage') && getProperty('release.stage') == 'SNAPSHOT'
def repo = isSnapshot ? 'oss-snapshot-local' : 'oss-release-local'
def artifactory_password = hasProperty("artifactory_password") ? getProperty("artifactory_password") : null
artifactory {
    contextUrl = "https://oss.jfrog.org/artifactory"
    if (null != artifactory_password) {
        publish {
            defaults {
                publications('main')
            }
            repository {
                repoKey = repo
                username = "harningt"
                password = artifactory_password
                maven = true
            }
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = "harningt"
            if (null != artifactory_password) {
                password = artifactory_password
            }
            maven = true
        }
        repository {
            repoKey = 'libs-snapshot'
            username = "harningt"
            if (null != artifactory_password) {
                password = artifactory_password
            }
            maven = true
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '17.+'

    testCompile localGroovy()
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3-SNAPSHOT'
}

wrapper {
    gradleVersion = '2.1'
}
