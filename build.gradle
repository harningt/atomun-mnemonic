import org.ajoberstar.gradle.git.release.opinion.Strategies

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'org.ajoberstar.release-opinion' version '1.5.1'
    id 'org.ajoberstar.defaults' version '0.6.0'
    id 'checkstyle'
    id 'findbugs'
    id "org.standardout.versioneye" version "1.4.0"
}

group = 'us.eharning.atomun'
description = 'Utility library implementing mnemonic <-> bytes conversions.'
sourceCompatibility = 1.7

defaults {
    id = 'harningt'

    developers = [
            [id: 'harningt', name: 'Thomas Harning Jr', email: 'harningt@gmail.com']
    ]

    // used by license plugin
    copyrightYears = '2016'
}

license {
    skipExistingHeaders true
}

release {
    // Adds SNAPSHOT strategy to allowed list to be used by Travis-CI
    versionStrategy Strategies.SNAPSHOT
}

repositories {
    jcenter()
    mavenCentral()
    // maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'http://www.tomgibara.com/maven/repo/' }
}

dependencies {
    findbugs 'com.google.code.findbugs:findbugs:3.0.1'

    compile 'us.eharning.atomun:atomun-core:0.0.2'

    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile 'com.google.code.findbugs:annotations:3.0.1'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.squareup.okio:okio:1.9.0'

    compile 'com.tomgibara.crinch:crinch-bits:0.7.2'

    testCompile 'org.codehaus.groovy:groovy:2.4.7'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'net.trajano.caliper:caliper:1.2.1'
    testCompile 'net.trajano.commons:commons-testing:2.1.0'
    testCompile 'org.yaml:snakeyaml:1.17'
    testCompile 'org.jooq:joor:0.9.6'
}

checkstyle {
    /* Using checkstyle 6.19 as that is the last version of Checkstyle to support JDK7 */
    toolVersion = '6.19'
    configFile = new File(rootDir, 'gradle/checkstyle/checkstyle.xml')
}

jacoco {
    toolVersion = '0.7.7.201606060606'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.destination "${buildDir}/jacocoHtml"
    }
}

sonarRunner {
    toolVersion = '2.4'
}

findbugs {
    sourceSets = [ sourceSets.main ]
}

javadoc {
    options.tags "todo:a:TODO:"
}

license {
    exclude "**/*.json"
    exclude "**/*.txt"
}

task runBenchmark(type: JavaExec, dependsOn: test) {
    main = 'us.eharning.atomun.mnemonic.IndexGeneratorBenchmark'
    classpath = sourceSets.test.runtimeClasspath
}

apply from: "$rootDir/gradle/maven-publish.gradle"

wrapper {
    gradleVersion = '2.14.1'
}
