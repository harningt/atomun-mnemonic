import org.ajoberstar.gradle.git.release.opinion.Strategies

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'org.ajoberstar.release-opinion' version '1.0.0'
    id 'org.ajoberstar.defaults' version '0.5.2'
    id 'findbugs'
}

group = 'us.eharning.atomun'
description = 'Utility library implementing mnemonic <-> bytes conversions.'
sourceCompatibility = 1.7

defaults {
    id = 'harningt'

    developers = [
            [id: 'harningt', name: 'Thomas Harning Jr', email: 'harningt@gmail.com']
    ]

    // used by license plugin
    copyrightYears = '2014, 2015'
}

configurations {
    // use local groovy
    all*.exclude group: 'org.codehaus.groovy'
}

release {
    // Adds SNAPSHOT strategy to allowed list to be used by Travis-CI
    versionStrategy Strategies.SNAPSHOT
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'http://www.tomgibara.com/maven/repo/' }
}

dependencies {
    findbugs 'com.google.code.findbugs:findbugs:3.0.0'

    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile 'com.google.code.findbugs:annotations:3.0.0'
    compile 'com.google.code.findbugs:jsr305:3.0.0'

    /* Benchmarks */
    compile 'com.tomgibara.crinch:crinch-bits:0.7.2'

    testCompile localGroovy()
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3-SNAPSHOT'
    testCompile 'net.trajano.caliper:caliper:1.2.0-SNAPSHOT'
}

findbugs {
    sourceSets = [ sourceSets.main ]
}

task runBenchmark(type: JavaExec, dependsOn: test) {
    def vmVer = System.getProperty('java.version')
    def osName = System.getProperty('os.name').replaceAll('\\s','')
    def osArch = System.getProperty('os.arch')
    def fnameBase = "ver${version}_${osName}-${osArch}_jvm${vmVer}"
    def benchMarksDir = "${project.buildDir}/benchmarks"
    ant.mkdir(dir: benchMarksDir)
    def outStream = new FileOutputStream("${benchMarksDir}/${fnameBase}-out.txt")
    standardOutput = outStream
    main = 'us.eharning.atomun.mnemonic.IndexGeneratorBenchmark'
    classpath = sourceSets.test.runtimeClasspath
    //args = ['--saveResults', "${benchMarksDir}/${fnameBase}.json", '-Jmode=-server,-client']
}

apply from: "$rootDir/gradle/maven-publish.gradle"

wrapper {
    gradleVersion = '2.1'
}
